---
title: Tutorial
description: Let's build your first application with Clojure Stack Lite
icon: Play
---

We just created a new project and ready to get started. Let's build a simple application.

## After generating project

First thing is recommended to do is checking the code formatting, linting, tests and outdated deps.
You will need [Docker](https://docs.docker.com/engine/install/) installed on your system as tests are running Testcontainers using Docker.

```shell
bb check
```

Now we can initiate git-repository and commit initial setup:

```shell
git init
git add .
git commit -am 'Initial commit'
```

## Start server

Navigate to project root and run REPL:

```shell
cd myproject
bb clj-repl
```

Then once we are in the REPL we can run the application system with server, database and TailwindCSS CLI components:

```shell
user=> (reset)
```

_TODO: add link to setup editor doc_

## Change starter page

Let's change something on the starter page and see the result. Open `src/myproject/views.clj` file
and replace ui component `:h1` to `[:h1 {:class ["text-5xl"]} "Hello world!"]`.
After reloading the page you will see that title has been changed on the web page.

![Clojure Stack Lite starter page](@/public/starter_hello.png)

## Application system components

The entry point of an application is `src/myproject/core.clj`. There is one function `main` that runs the system with production profile.
We use Integrant as a framework for running all components of the system. Core components are `db` and `server` and they are defined in
`src/myproject/db.clj` and `src/myproject/server.clj` appropriately. The defualt configuration of server component is described in the
separate library `reitit-extras`, this is just shortcut for convenience. It has few options to extend server configuration.
If you want to change or replace amything in the server configuration, just copy server definition from `reitit-extras` to your project.

In development mode entry point is `dev/user.clj`. There we start the same system with dev profile and an extension of config `resources/config.dev` that contains
just one additional component to start `tailwindcss` CLI tool in watch mode to auto-reload css styles on file change. So it's started as part of the application system and we don't need to
run it separately.

## The application

All set up and we are ready to create our application. As I mentioned earlier we are going to build movie list application called "Movies Lite".
There will be an ability to add a new movie, show the list of movies and delete a row from the list.

## Database structure

For simplicity we will create one table called `movie` with fields `id`, `title`, `year`, `director`.
To create this schema please add following database migration by creating a new file `resources/migrations/0002.up.sql` with content:

```sql title="resources/migrations/0002.up.sql"
CREATE TABLE movie (
    id INTEGER UNIQUE PRIMARY KEY AUTOINCREMENT NOT NULL,
    title TEXT NOT NULL,
    year INTEGER NOT NULL,
    director TEXT NOT NULL
);
```

Migrations are applied as part of the system start. So, to apply our new migrations we can run `(reset)` in the REPL.
After it we can connect to our database using any SQLite client (for example [TablePlus](https://tableplus.com/)) and check that we actually created the table `movie`:

![Movies DB table](@/public/lite_movies_db_table.png)

## Application UI layout

To visualise all parts of the application let's create a UI layout for our main page.
Let's look at `src/myproject/views.clj`. This namespace contains functions that converts data to Hiccup templates.
That we will convert to HTML at rendering response step. There is a `base` function that contains default setup for all pages
with page title, static files and a placeholder for any content we want to show on the page.

Mockup of the page we are going to build looks like:

![UI mockup](@/public/lite_mockup.png)

There is a page title and a table with list of movies. Each row has button "Delete" at the last column.
At the bottom of the table there is a form to create a new movie.

Our `home-page` function and additional functions will look like:

```clojure title="src/myproject/views.clj"
; ...
(defn list-item
  [{:keys [movie]}]
  [:tr
   [:td {:class ["px-6" "py-4" "text-gray-800"]} (:title movie)]
   [:td {:class ["px-6" "py-4" "text-gray-800"]} (:year movie)]
   [:td {:class ["px-6" "py-4" "text-gray-800"]} (:director movie)]
   [:td {:class ["px-6" "py-4"]}
    [:button
     {:class ["text-red-400" "hover:bg-gray-50" "bg-white" "border"
              "border-gray-300" "rounded-md" "px-3" "py-1" "cursor-pointer"]}
     "Delete"]]])

(defn form-input
  [{:keys [field-name field-type attrs]}]
  [:div
   {:class ["flex" "flex-col"]}
   [:input (merge {:class ["w-full" "border" "border-gray-300" "rounded-md" "px-3" "py-2"]
                   :type field-type
                   :name field-name
                   :placeholder (str "Enter " field-name)}
                  attrs)]])

(defn form
  []
  [:form
   (cond-> {:id "form-create-movie"
            :class ["border-t" "border-gray-200" "bg-gray-50" "p-6"]})
   [:div {:class ["grid" "grid-cols-1" "md:grid-cols-4" "gap-4"]}
    (form-input {:field-name "title"
                 :field-type "text"})
    (form-input {:field-name "year"
                 :field-type "number"
                 :attrs {:min 1888}})
    (form-input {:field-name "director"
                 :field-type "text"})
    [:div {:class ["flex" "flex-col"]}
     [:button {:class ["bg-blue-600" "text-white" "rounded-md" "px-4" "py-2"
                       "hover:bg-blue-700" "cursor-pointer"]}
      "Create"]]]])

(defn home-page
  [{:keys [movies]}]
  (base
    [:div {:id "content"
           :class ["container" "mx-auto" "p-6" "max-w-4xl"]}
     [:div {:class ["mb-10" "flex" "justify-between" "items-center"]}
      [:h1 {:class ["text-2xl" "font-semibold" "text-gray-800"]} "Movies Lite"]]
     [:div {:class ["bg-white" "rounded-lg" "shadow-md" "overflow-hidden" "border"
                    "border-gray-200"]}
      [:div {:class ["overflow-x-auto"]}
       [:table {:class ["min-w-full" "divide-y" "divide-gray-200"]}
        [:thead {:class ["bg-white"]}
         [:tr
          [:th {:class ["px-6" "py-3" "text-left" "text-gray-500" "font-medium"]
                :scope "col"} "Title"]
          [:th {:class ["px-6" "py-3" "text-left" "text-gray-500" "font-medium"]
                :scope "col"} "Year"]
          [:th {:class ["px-6" "py-3" "text-left" "text-gray-500" "font-medium"]
                :scope "col"} "Director"]
          [:th {:class ["px-6" "py-3" "text-left" "text-gray-500" "font-medium"]
                :scope "col"} "Actions"]]]
        [:tbody {:id "table-content"
                 :class ["bg-white" "divide-y" "divide-gray-200"]}
         (for [movie movies]
           (list-item {:movie movie}))]]]
      (form)]]))
```

We also moved list item, form and form inputs to separate components for convenience and as we will need to rendered it separately for created movie
in the next section.

We need to update our `home-handler` to pass fake data for `movies` to show them on the page:

```clojure title="src/myproject/handlers.clj"
; ...
(defn home-handler
  [_]
  (-> {:movies [{:title "Movie 1"
                 :year 2023
                 :director "Director 1"}
                {:title "Movie 2"
                 :year 2022
                 :director "Director 2"}]}
      (views/home-page)
      (reitit-extras/render-html)))
```

After refreshing page in the browser it will look like:

![Page layout](@/public/lite_page_layout.png)

## Create movie

We have a ui part for the table and form. Now we can add a new route and handler for creating a movie.

First, we add an sql queries for creating and fetching a movies. Let's create a new file `queries.clj`:

```clj title="src/myproject/queries.clj"
(ns myproject.queries
  (:require [myproject.db :as db]))

(defn get-movies
  [db]
  (db/exec! db {:select [:*]
                :from [:movie]
                :order-by [:id]}))

(defn create-movie
  [db {:keys [title year director]}]
  (->> {:insert-into :movie
        :values [{:title title
                  :year year
                  :director director}]
        :returning [:*]}
       (db/exec-one! db)))
```

Now we can add a new handler for creating a movie:

```clojure title="src/myproject/handlers.clj"
(ns myproject.handlers
  (:require [myproject.queries :as queries]
            ; ...
            )

; ...

(defn create-movie-handler
  "Render a new table item with newly created movie."
  [{{:keys [db]} :context
    {:keys [form]} :parameters
    router :reitit.core/router}]
  (-> {:router router
       :movie (queries/create-movie db form)}
      (views/list-item)
      (reitit-extras/render-html)))
```

Single parameter of a router is `request`.
Each handler will have a key `context` add to request by default and a router.
Context has all system components of our application, so we can use a `db` component with connection
pool to create a new movie.

Now let's ad a new route for this handler.

```clj title="src/myproject/routes.clj"
; ...

(def routes
  [; ...
   ["/movies"
    ["" {:name ::movies
         :post {:handler handlers/create-movie-handler
                :responses {200 {:body string?}}}}]]])
```

And finaly, we need to update our form such that it will send request to our new route:

```clj title="src/myproject/views.clj"
(defn home-page  // [!code highlight]
  // [!code highlight]
  [{:keys [router movies]}]
  (base
    [:div {:id "content"
           :class ["container" "mx-auto" "p-6" "max-w-4xl"]}
      ;...
      [:form
       {:class ["border-t" "border-gray-200" "bg-gray-50" "p-6"]
        // [!code highlight]
        :hx-post (reitit-extras/get-route router ::routes/movies-list)
        // [!code highlight]
        :hx-target "#table-content"
        // [!code highlight]
        :hx-swap "beforeend"}
       // [!code highlight]
       (reitit-extras/csrf-token)
       [:div {:class ["grid" "grid-cols-1" "md:grid-cols-4" "gap-4"]}
        ; ...
        [:div {:class ["flex" "items-end" "grow" "ml-10"]}
         [:button {:class ["bg-blue-600" "text-white" "rounded-md" "px-4" "py-2"
                           "hover:bg-blue-700" "cursor-pointer"]}
          "Create"]]]]]]))
```

We need to pass router to the page view function to be able to build an url for creating a movie.
Also we need to pass movies list to be able to render the table. So we convert our var `home-page`
to function and pass parameters.

Then we update form with `:hx-port` attribute with new route, `:hx-target` with the id of a table content
that we added at previous step. We are going to return HTML for just one item in the table, so we configure
`:hx-swap "beforeend"` to add it as the very last item.

To be able to send `POST` request we added a hidden input with csrf-token.

And last thing. To show the home page properly let's update the handler for it with passing new parameters:

```clj title="src/myproject/handlers.clj"
;...
(defn home-handler
  [{{:keys [db]} :context
    router :reitit.core/router}]
  (-> {:router router
       :movies (queries/get-movies db)}
      (views/home-page)
      (reitit-extras/render-html)))
;...
```

Now once we reload the system in REPL `(reset)` we should be able to create a new moview using our form.

![Create movie](@/public/lite_create.png)

## Form errors

Our for can create a movie but what if we don't fill title? It will throw an error, because this field is required in the database.
Let's add parameters validation before inserting data to the database. We also need showing validation errors on the page.

Usually, we would describe params validation as part of router using Malli, but to be able to return different HTML with of a form with validation errors
we need to move this validation to the handler itself.



## Delete a movie

## Summary